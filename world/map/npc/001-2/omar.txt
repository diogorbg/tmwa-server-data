//
function|script|KadiyaSubquestConsts|,
{
    set @Q_kadiya_status, (QUEST_MAGIC2 & NIBBLE_3_MASK) >> NIBBLE_3_SHIFT;

    set @Q_STATUS_NONE, 0;
    set @Q_STATUS_KNOWS_MOPOX, 1;
    set @Q_STATUS_MADE_MOPOX, 2;
    set @Q_STATUS_DIDNT_DRINK, 3;
    set @Q_STATUS_WANTS_CHOCOCAKE, 4;
    set @Q_STATUS_WANTS_ORANGECUPCAKE, 5;
    set @Q_STATUS_COMPLETED, 6;
    set @Q_STATUS_COMPLETED_ELANORE, 7;
    set @Q_STATUS_COMPLETED_NOELANORE, 8;
    set @Q_STATUS_COMPLETED_POST_ELANORE, 9;

    if (((QUEST_MAGIC2 & NIBBLE_1_MASK) >> NIBBLE_1_SHIFT) < 4) set @Q_kadiya_status, @Q_STATUS_NONE;

    if (((QUEST_MAGIC2 & NIBBLE_1_MASK) >> NIBBLE_1_SHIFT) > 4) set @Q_kadiya_status, @Q_STATUS_COMPLETED_POST_ELANORE;

    return;
}

001-2.gat,70,60,0|script|Omar|162,
{
    callfunc "ElanoreFix";
    callfunc "KadiyaSubquestConsts";

    set @Q_status, @Q_kadiya_status;

    if (@Q_status >= @Q_STATUS_COMPLETED) goto L_cured;

    if (@Q_status == @Q_STATUS_DIDNT_DRINK) goto L_didnt_drink;

    if (@Q_status > @Q_STATUS_DIDNT_DRINK) goto L_make_food;

    mes "[Omar]";
    mes "\"Bem vindo a minha humilde casa.\"";
    next;
    menu
        "Prazer em conhecer! Posso saber quem você é?", L_omar,
        "Qual o problema com a sua filha?", L_kadiya_sick,
        "Obrigado!", L_Close;

L_omar:
    mes "[Omar]";
    mes "\"Meu nome é Omar. E eu sou um negociante de óleos e especiarias.\"";
    goto L_Close;

L_kadiya_sick:
    mes "[Omar]";
    mes "\"Kadiya pegou Ponderpox, eu temo.\"";
    mes "ele suspira.";
    mes "\"Ela tem ficado febril por muitos dias. Eu gostaria que houvesse algo que eu pudesse fazer.\"";
    next;
    menu
        "Você já perguntou a Elanore a curadeira?", L_sick_elanore,
        "Que tal ir ao hospital de Hurnscald?", L_sick_hospital,
        "Posso ajudar?", L_sick_self,
        "Eu sinto muito em ouvir isso.", L_Next;

L_Next:
    mes "[Omar]";
    mes "\"Bem, ela é uma garota forte. Tenho certeza que ela vai superar isso com um tempo.\"";
    mes "Ele sorri, mas você vê dúvida em seus olhos.";
    goto L_Close;

L_sick_elanore:
    mes "[Omar]";
    mes "\"Elanore?\"";
    mes "Ele franze a testa.";
    mes "\"Ela não sabe de nada. Não vejo razão em falar com ela.\"";
    goto L_Close;

L_sick_hospital:
    mes "[Omar]";
    mes "\"Hurnscald é muito longe. Eu não acho que seria bom para ela para ir em uma viagem tão longa.\"";
    goto L_Close;

L_sick_self:
    mes "[Omar]";
    mes "\"Você é muito gentil, mas eu não acho que há qualquer coisa que você possa fazer.\"";
    goto L_Close;

L_didnt_drink:
    mes "[Omar]";
    mes "\"Então ela tem Mopox, não Ponderpox, você diz?  Hmm. Isso é muito mais difícil de curar, eu acho.\"";
    mes "\"Sou grato pela esforço que você colocou na fabricação da poção de cerveja para ela. Mas cheira mau...\"";
    next;
    mes "[Omar]";
    mes "\"Eu prometi a minha querida esposa que eu nunca faria Kadiya comer ou beber qualquer coisa que ela não gosta. Então eu não vou forçá-la a beber isso.\"";
    next;
    mes "[Omar]";
    set @Q_status, (@Q_STATUS_WANTS_CHOCOCAKE + rand(2));
    callsub S_update_var;
    if (@Q_status == @Q_STATUS_WANTS_ORANGECUPCAKE)
        mes "\"Se pudéssemos fazê-lo sentir o cheiro e gosto de bolinhos de laranja... ela realmente ama aqueles cupcakes.\"";
    if (@Q_status == @Q_STATUS_WANTS_CHOCOCAKE)
        mes "\"Se apenas isso fosse um bolo de chocolate e não uma poção... ela realmente ama bolos de chocolate.\"";
    goto L_Close;

L_make_food:
    mes "[Omar]";
    mes "\"Eu deveria ficar aqui para vigiá-la. Talvez eu possa pensar em uma maneira de convencê-la a beber a poção...\"";
    if (@Q_status == @Q_STATUS_WANTS_ORANGECUPCAKE)
        mes "\"Infelizmente, não é um cupcake de laranja.\"";
    if (@Q_status == @Q_STATUS_WANTS_CHOCOCAKE)
        mes "\"Se tivesse o gosto de bolo de chocolate, que é seu favorito ...\"";
    goto L_Close;

L_cured:
    if (@Q_status == @Q_STATUS_COMPLETED) goto L_cured_choice;
    mes "[Omar]";
    mes "\"Ela está dormindo agora, mas ela parece estar muito melhor. Estou certo que ela vai ficar bem e voltar a correr em breve.\"";
    goto L_Close;

L_cured_choice:
    mes "[Omar]";
    mes "\"Sou muito grato por sua ajuda. Eu realmente tinha certeza de que era Ponderpox, não Mopox, que ela tinha.\"";
    next;
    menu
        "Oh, não foi nada.", L_cured_nothing,
        "Eu não fiz isso sozinho; Elanore me ajudou.", L_cured_elanore,
        "Isso vai custar 5000 GP.", L_Next1;

L_Next1:
    mes "[Omar]";
    mes "\"Ah, com certeza.\"";
    mes "Ele entrega-lhe um pequeno saco de dinheiro.";
    next;
    set @Q_status, @Q_STATUS_COMPLETED_NOELANORE;
    callsub S_update_var;
    set Zeny, Zeny + 5000;
    goto L_Close;

L_cured_nothing:
    mes "[Omar]";
    mes "\"Não, isso não é nada. Ela é tudo para mim. Aqui, você merece uma recompensa.\"";
    mes "Ele entrega-lhe um saco de dinheiro contendo 10.000 GP e duas pérolas.";
    mes "\"Muito Obrigado!\"";
    set @Q_status, @Q_STATUS_COMPLETED_NOELANORE;
    callsub S_update_var;
    getitem "Pearl", 2;
    set Zeny, Zeny + 10000;
    goto L_Close;

L_cured_elanore:
    mes "[Omar]";
    mes "Omar com olhar severo.";
    mes "\"Essa bruxa? Você trabalhou com ela sem me dizer?\"";
    mes "Ele olha para a sua filha, de repente com olhar de preocupação.";
    mes "\"Embora, ela estar melhor...\"";
    next;
    mes "[Omar]";
    mes "Omar balança a cabeça.";
    mes "\"Vou ter que pensar sobre isso. Por favor, saia daqui!\"";
    next;
    set @Q_status, @Q_STATUS_COMPLETED_ELANORE;
    callsub S_update_var;
    goto L_Close;

L_Close:
    set @Q_STATUS_KNOWS_MOPOX, 0;
    set @Q_STATUS_MADE_MOPOX, 0;
    set @Q_STATUS_DIDNT_DRINK, 0;
    set @Q_STATUS_WANTS_CHOCOCAKE, 0;
    set @Q_STATUS_WANTS_ORANGECUPCAKE, 0;
    set @Q_STATUS_COMPLETED, 0;
    set @Q_STATUS_COMPLETED_ELANORE, 0;
    set @Q_STATUS_COMPLETED_NOELANORE, 0;
    set @Q_STATUS_COMPLETED_POST_ELANORE, 0;
    set @Q_status, 0;
    close;


S_update_var:
    set QUEST_MAGIC2, (QUEST_MAGIC2 & ~(NIBBLE_3_MASK) | (@Q_status << NIBBLE_3_SHIFT));
    return;
}

001-2.gat,76,59,0|script|Kadiya|174,
{
    set @child_number, 7;
    callfunc "XmasList";

    callfunc "ElanoreFix";
    set @Q_MASK, NIBBLE_3_MASK;
    set @Q_SHIFT, NIBBLE_3_SHIFT;
    callfunc "KadiyaSubquestConsts";
    set @Q_status, @Q_kadiya_status;
    if (@Q_status >= @Q_STATUS_COMPLETED) goto L_cured;

    mes "Você vê uma garota deitada na cama. No início, ela não parece perceber que você se aproxima.";
    mes "Por fim, ela vira a cabeça para você. Ao perceber pelo suor na testa e olhar em seus olhos, ela está sofrendo de algum tipo de febre.";
    next;
    mes "[Kadiya]";
    mes "\"Olá,\" ela diz com uma voz pequena.";
    next;
    set @M_NAME, 1;
    set @M_CANDY, 2;
    set @M_POTION, 3;
    set @M_CHOCOCAKE, 4;
    set @M_CUPCAKE, 5;

    setarray @choice$, "Olá! Qual o seu nome?", "Gostaria de um pouco de doces?", "", "", "", "", "", "";
    setarray @choice_idx, @M_NAME, @M_CANDY, 0, 0, 0, 0, 0, 0;
    set @choices_nr, 2;

    if (countitem("MopoxCurePotion") == 0) goto L_M_no_cure;

    set @choice_idx[@choices_nr], @M_POTION;
    set @choice$[@choices_nr], "Esta poção irá curar a sua doença!";
    set @choices_nr, @choices_nr + 1;
    goto L_M_no_cure;

L_M_no_cure:
    if ((countitem("LacedChocolateCake") == 0) || (@Q_status != @Q_STATUS_WANTS_CHOCOCAKE))
        goto L_M_no_chococake;
    set @choice_idx[@choices_nr], @M_CHOCOCAKE;
    set @choice$[@choices_nr], "Gostaria de um bolo especial de chocolate?";
    set @choices_nr, @choices_nr + 1;
    goto L_M_no_chococake;

L_M_no_chococake:
    if ((countitem("LacedOrangeCupcake") == 0) || (@Q_status != @Q_STATUS_WANTS_ORANGECUPCAKE))
        goto L_M_no_cupcake;
    set @choice_idx[@choices_nr], @M_CUPCAKE;
    set @choice$[@choices_nr], "Gostaria de um cupcake especial de laranja?";
    set @choices_nr, @choices_nr + 1;
    goto L_M_no_cupcake;

L_M_no_cupcake:
    set @choice_idx[@choices_nr], 0;
    set @choice$[@choices_nr], "Tchau.";
    set @choices_nr, @choices_nr + 1;

    menu
        @choice$[0], L_MenuItems,
        @choice$[1], L_MenuItems,
        @choice$[2], L_MenuItems,
        @choice$[3], L_MenuItems,
        @choice$[4], L_MenuItems,
        @choice$[5], L_MenuItems;

L_MenuItems:
    set @choice, @choice_idx[@menu - 1];

    if (@choice == @M_NAME) goto L_name;
    if (@choice == @M_CANDY) goto L_no_candy;
    if (@choice == @M_POTION) goto L_potion;
    if (@choice == @M_CHOCOCAKE) goto L_chococake;
    if (@choice == @M_CUPCAKE) goto L_cupcake;
    goto L_Close;

L_name:
    mes "[Kadiya]";
    mes "Ela sorri um sorriso fraco.";
    mes "\"Meu nome é Kadiya.\"";
    goto L_Close;

L_no_candy:
    mes "[Kadiya]";
    mes "Ela hesita por um momento.";
    mes "\"Não, obrigado. Eu acho que não deveria.\"";
    goto L_Close;

L_potion:
    mes "[Kadiya]";
    if (@Q_status < @Q_STATUS_DIDNT_DRINK) set @Q_status, @Q_STATUS_DIDNT_DRINK;
    callsub S_update_var;

    mes "Kadiya faz uma careta e empurra a garrafa.";
    mes "\"Isso cheira muito mau!\"";
    next;
    mes "[Kadiya]";
    mes "Ela puxa os lençóis sobre sua cabeça.";
    mes "\"Eu não vou beber isso!\"";
    goto L_Close;

L_chococake:
    delitem "LacedChocolateCake", 1;
    goto L_do_cure;

L_cupcake:
    delitem "LacedOrangeCupcake", 1;
    goto L_do_cure;

L_do_cure:
    if (@Q_status < @Q_STATUS_COMPLETED) set @Q_status, @Q_STATUS_COMPLETED;
    callsub S_update_var;

    mes "[Kadiya]";
    mes "Os olhos de Kadiya se abrem.";
    mes "\"Oh! Papai, está tudo bem se eu comer isso?\"";
    mes "Omar sorridente acenou com a cabeça. \"Você deve comer um pouco para recuperar a sua força, querida.\"";
    next;
    mes "[Kadiya]";
    mes "Kadiya devora seu presente em poucas mordidas.";
    next;
    mes "[Kadiya]";
    mes "ela sorri. \"Obrigado! Já me sinto melhor!\".";
    next;
    mes "[Kadiya]";
    mes "Ela parece muito mais saudável, Omar caminha e toca a testa dela.";
    mes "\"Agora que é engraçado – sua febre baixou um pouco!\"";
    mes "Ele sorri para você. \"Obrigado pela ajuda!\"";
    next;
    mes "[Omar]";
    mes "\"Você deveria dormir um pouco agora, querida. Tenho certeza que você ficará melhor em breve.\"";
    goto L_Close;

L_cured:
    mes "[Kadiya]";
    mes "Kadiya parece estar dormindo calmamente. Ela parece muito menos suada do que antes; a menos que você esteja enganado, a febre desapareceu.";
    goto L_Close;

L_Close:
    set @Q_status, 0;
    set @Q_MASK, 0;
    set @Q_SHIFT, 0;
    set @M_NAME, 0;
    set @M_CANDY, 0;
    set @M_POTION, 0;
    set @M_CHOCOCAKE, 0;
    set @M_CUPCAKE, 0;
    set @choice, 0;
    set @choices_nr, 0;
    cleararray @choice_idx[0], 0, 7;
    cleararray @choice$[0], "", 7;
    close;

S_update_var:
    set QUEST_MAGIC2, (QUEST_MAGIC2 & ~(@Q_MASK) | (@Q_status << @Q_SHIFT));
    return;
}
