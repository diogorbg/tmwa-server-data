//
// traduzido por Italo Luna


001-2.gat,34,23,0|script|Lider da Trupe|165,
{
    callfunc "ClearVariables";

    set @inspector, ((QUEST_Hurnscald & NIBBLE_3_MASK) >> NIBBLE_3_SHIFT);

    if (@inspector == 4) goto L_NohMask_Troupe;
    if (@inspector == 13) goto L_NohMask_Found;

    mes "[Líder da Trupe]";
    mes "\"Olá. Eu sou o líder de uma companhia de teatro. Nós vamos ficar aqui em Tulimshar por um tempo.\"";
    goto L_Close;

L_NohMask_Troupe:
    mes "[Líder da Trupe]";
    mes "\"Sim, uma máscara nos foi roubado na última noite, estávamos em Hurnscald.\"";
    next;
    menu
        "Alguma idéia de quem poderia ter tomado?", L_NohMask_Idea,
        "Tem a certeza de um de seus integrantes da trupe não escondê-lo e cometer esses roubos?", L_NohMask_Accuse,
        "Hmm...", L_Close;

L_NohMask_Idea:
    set @inspector, 5;
    callsub S_Update_Mask;
    mes "[Líder da Trupe]";
    mes "\"Hm...Eu vi um homem velho sair perto do teatro depois de nosso último show.\"";
    goto L_Close;

L_NohMask_Accuse:
    mes "[Líder da Trupe]";
    mes "\"Estou absolutamente positivo. Não é da minha companhia, deixaram a cidade desde que chegamos aqui. bom dia!\"";
    goto L_Close;

L_NohMask_Found:
    mes "[Líder da Trupe]";
    mes "\"Obrigado por encontrar a máscara. Você fez um bom trabalho, você deve mantê-lo.\"";
    getinventorylist;
    if (@inventorylist_count == 100) goto L_NohMask_TooMany;
    mes "[1500 experience points]";
    getexp 1500, 0;
    set @inspector, 14;
    callsub S_Update_Mask;
    getitem "NohMask", 1;
    next;
    mes "[Líder da Trupe]";
    mes "\"Nós não precisamos de mais. Estamos fazendo vários shows aqui.\"";
    goto L_Close;

L_NohMask_TooMany:
    next;
    mes "[Líder da Trupe]";
    mes "\"Exceto que, você não se parece ter qualquer espaço para isso. Vou guardá-la para você até que você tenha espaço.\"";
    goto L_Close;

L_Close:
    set @inspector, 0;
    close;

S_Update_Mask:
    set QUEST_Hurnscald, (QUEST_Hurnscald & ~(NIBBLE_3_MASK)) | (@inspector << NIBBLE_3_SHIFT);
    return;
}
