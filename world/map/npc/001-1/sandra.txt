// Variables used: nibble 3 of QUEST_SouthTulimshar

001-1.gat,110,71,0|script|Sandra|114,
{
    // This NPC previously used the variable TMW_Quest
    callfunc "ClearVarTMW_Quest";

    set @state, ((QUEST_SouthTulimshar & NIBBLE_3_MASK) >> NIBBLE_3_SHIFT);

    if (@state >= 2) goto L_Done;
    if (@state == 1) goto L_Progress;

    mes "[Sandra]";
    mes "\"Caçar monstros para conseguir ingredientes de poções às vezes pode ser uma tarefa difícil.\"";
    next;

    set @TEMP, rand(4);
    if(@TEMP == 1) goto L_Opening1;
    if(@TEMP == 2) goto L_Opening2;
    if(@TEMP == 3) goto L_Opening3;
    goto L_Opening0;

L_Opening0:
    mes "\"Nos arredores de Tulimshar, há alguns escorpiões ... Preciso de ajuda! Você vai me ajudar?\"";
    next;
    goto L_Ask;

L_Opening1:
    mes "\"Quando você se arrisca nos lugares estranhos da Tulimshar, você pode ver escorpiões. Vai me ajudar a matar alguns?\"";
    next;
    goto L_Ask;

L_Opening2:
    mes "\"O Ferrão de Escorpião carrega muitas propriedades utilizadas em poções. Pode obter alguns para mim?\"";
    next;
    goto L_Ask;

L_Opening3:
    mes "\"Você parece robusto o suficiente, vai me ajudar a conseguir algo?\"";
    next;
    goto L_Ask;

L_Ask:
    menu
        "Sim.", L_Yes,
        "Não.", L_Close;

L_Yes:
    set @TEMP, rand(3);
    if(@TEMP == 1) goto L_Req1;
    if(@TEMP == 2) goto L_Req2;
    goto L_Req0;

L_Req0:
    mes "[Sandra]";
    mes "\"Eu preciso de 5 Ferrões de Escorpião.\"";
    next;
    goto L_Set;

L_Req1:
    mes "[Sandra]";
    mes "\"Ouvi há pouco que ferrões de escorpiões podem ser usado para fins médicos. Eu preciso de você para me ajudar a obter 5 Ferrões de Escorpião.\"";
    next;
    goto L_Set;

L_Req2:
    mes "[Sandra]";
    mes "\"Traga-me 5 Ferrões de Escorpião. Vou dar-lhe alguma coisa, se você fizer isso!\"";
    next;
    goto L_Set;

L_Set:
    set @state, 1;
    callsub S_Update_Var;
    mes "\"Por favor, traga-os para mim!\"";
    goto L_Close;

L_Progress:
    if (countitem("ScorpionStinger") < 5)
        goto L_NotEnough;
    mes "[Sandra]";
    mes "\"Exelente! Ve me trouxe os 5 Ferrões de Escorpião!";
    mes "Tome algo para você.\"";
    getinventorylist;
    if (@inventorylist_count + (countitem("ScorpionStinger") == 5) - (countitem("Arrow") == 0) > 99)
        goto L_TooMany;
    delitem "ScorpionStinger", 5;
    getitem "Bow", 1;
    getitem "Arrow", 100;
    set @state, 2;
    callsub S_Update_Var;
    goto L_Close;

L_NotEnough:
    mes "[Sandra]";
    mes "\"Por favor volte e me traga os 5 Ferrões de Escorpião.\"";
    goto L_Close;

L_Done:
    mes "[Sandra]";
    mes "\"Obrigado pela ajuda!\"";
    goto L_Close;

L_Close:
    set @TEMP, 0;
    set @state, 0;
    close;

L_TooMany:
    next;
    mes "[Sandra]";
    mes "\"Você não tem espaço para a minha recompensa. Eu vou esperar até que você tenha.\"";
    goto L_Close;

S_Update_Var:
    set QUEST_SouthTulimshar, (QUEST_SouthTulimshar & ~(NIBBLE_3_MASK) | (@state << NIBBLE_3_SHIFT));
    return;
}
