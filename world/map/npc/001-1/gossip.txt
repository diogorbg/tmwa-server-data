// NPC to provide hints on progression of linear quest chain in Tulimshar
// traduzido por Ernando Quirino


001-1.gat,49,31,0|script|Gladys|154,
{
    // This NPC previously used the variable TMW_Quest
    callfunc "ClearVarTMW_Quest";

    set @guards, ((QUEST_SouthTulimshar & BYTE_2_MASK) >> BYTE_2_SHIFT);
    set @bermik, ((QUEST_SouthTulimshar & NIBBLE_0_MASK) >> NIBBLE_0_SHIFT);
    set @sarah, ((QUEST_SouthTulimshar & NIBBLE_1_MASK) >> NIBBLE_1_SHIFT);
    set @vincent, ((QUEST_SouthTulimshar & NIBBLE_2_MASK) >> NIBBLE_2_SHIFT);
    set @sandra, ((QUEST_SouthTulimshar & NIBBLE_3_MASK) >> NIBBLE_3_SHIFT);

    mes "[Gladys Fofoqueira]";
    mes "\"Olá! Ouço alguns dos rumores mais fascinantes e pedaços de notícias. Acabei de ouvir um pouco mais hoje!\"";
    mes "\"Gostaria que ue lhe contasse algumas?\"";
    menu
        "Claro!", L_Yes,
        "Não obrigado.", L_No;

L_Yes:
    if (@bermik < 4)  goto L_Intro;
    if (@bermik < 6)  goto L_Mik;
    if (@sarah < 2)   goto L_Sarah;
    if (@vincent < 2) goto L_Vinc;
    if (@sandra < 2)  goto L_Sandra;
    if (@guards < 28) goto L_Desert;
    goto L_Ferry;

L_No:
    mes "[Gladys Fofoqueira]";
    mes "\"Tudo bem. Volte quando quiser. Eu sempre tenho alguns pedaços de notícias!\"";
    goto L_Close;

L_Intro:
    mes "[Gladys Fofoqueira]";
    mes "\"Já conheceu nosso capitão da guarda? Ele geralmente mantém um posto do lado de fora dos portões, onde ele pode manter um olho nas coisas. Ele gosta de falar com novos aventureiros também.\"";
    next;
    mes "[Gladys Fofoqueira]";
    mes "\"Eu também ouvi algumas das crianças na praça falando hoje. Eu acho que eles estão tendo problemas com algumas das suas tarefas. As crianças hoje em dia...\"";
    next;
    mes "A velha senhora começa a murmurar para si mesma sobre colinas e neve. Talvez seja melhor deixá-la sozinha por enquanto.";
    goto L_Close;

L_Mik:
    mes "[Gladys Fofoqueira]";
    mes "\"Esse rapaz agradável Bernard deu-me um pouco da sopa que você ajudou a fazer. Foi agradável!\"";
    next;
    mes "[Gladys Fofoqueira]";
    mes "\"Uma das senhoras em meu clube de costura tinha mencionado dar uma tarefa para seu neto. Eu tenho minhas dúvidas sobre ele jer terminado...\"";
    goto L_Close;

L_Sarah:
    mes "[Gladys Fofoqueira]";
    mes "\"Ethel, a senhora no meu clube de custura, me disse que você ajudou seu neto com a sua tarefa. Isso foi muito simpático de sua parte!\"";
    next;
    mes "[Gladys Fofoqueira]";
    mes "\"Você acredita no que os pais deixam seus filhos fazer hoje em dia ?! Eu ouvi sobre essa garota no outro lado da cidade que nada faz além de comer doces todos os dias! Honestamente, onde é que o mundo vai parar?\"";
    goto L_Close;

L_Vinc:
    mes "[Gladys Fofoqueira]";
    mes "\"Acabei de receber a notícia de uma oportunidade de negócio novo e excitante! Não é ser ser um novo empresário na cidade fazendo brinquedos. Você pode entrar nessa enquanto está dando lucro!\"";
    goto L_Close;

L_Sandra:
    mes "[Gladys Fofoqueira]";
    mes "\"Como é que as empresas de brinquedos trabalham lá fora? Muito boa dica, hein?\"";
    next;
    mes "[Gladys Fofoqueira]";
    mes "\"Eu tenho ouvido relatos de que toda a cidade! Eu acho que há apenas uma menina que não tem ajudado, mas ela é uma estranha ... sempre mexe com insetos. Eca!\"";
    goto L_Close;

L_Desert:
    if (Sex == 0) set @person$, "moça";
    if (Sex == 1) set @person$, "rapaz";

    mes "[Gladys Fofoqueira]";
    mes "\"Veja se não é " + strcharinfo(0) + "!  Nosso herói local e super agradável " + @person$ + ".\"";
    mes "\"The whole town is buzzing about you now!\"";
    next;
    mes "[Gladys Fofoqueira]";
    mes "\"Se você ainda está à procura de aventura, gostaria de sugerir a falar com alguns dos guardas. Ouvi dizer que há uma ameaça monstro fora dos muros da cidade!\"";
    goto L_Close;

L_Ferry:
    mes "[Gladys Fofoqueira]";
    mes "Ouvi dizer que há um marinheiro bonito no norte da cidade, que vai levar as pessoas a novos lugares exóticos! Estou guardando meus tostões!\"";
    goto L_Close;

L_Close:
    set @person$, "";
    set @guards, 0;
    set @bermik, 0;
    set @sarah, 0;
    set @vincent, 0;
    set @sandra, 0;
    close;
}
