// Teaches Trading skill
// Offers item conversion for non-magic users
// Updated by: TheKandiman
// Reviewed by: Wombat
// traduzido por Ernando Quirino


001-1.gat,57,31,0|script|Negociante|115,
{
    set @Ironprice, 1000;
    set @Sulphurprice, 1200;
    set @Potionprice, 500;
    if ((getskilllv(SKILL_TRADE) > 0) && BaseLevel >= 25) goto L_Trade;
    if (getskilllv(SKILL_TRADE) > 0) goto L_Later;

    mes "[Negociante]";
    mes "\"Olá! Eu estou aqui para negociar com as pessoas de Tulim, porém eu não tenho nada agora.\"";
    next;
    menu
        "Oh. Então eu vou embora.", L_Close,
        "Você não tem nada?", L_More;

L_More:
    mes "[Negociante]";
    mes "\"Não. Não tenho nada para você. Exerto...\"";
    next;
    menu
        "Sim?", L_Except,
        "Ok então.",  L_Close;

L_Except:
    mes "[Negociante]";
    mes "\"Eu posso te ensinar como negociar. Mas vai te custar 5GP.\"";
    next;
    menu
        "Aceito", L_Teach,
        "Não obrigado.", L_Close;

L_Teach:
    if (Zeny < 5) goto L_NotEnoughMoney;
    set Zeny, Zeny - 5;
    setskill SKILL_TRADE, 1;
    mes "[Negociante]";
	mes "\"Você pode iniciar uma negociação clicando o segundo botão do mouse sobre alguém. Quando aparecer um menu escolha a opção [Trade] ou [Negociar com...].";
	mes "Você terá que adicionar os itens que está vendendo à caixa de Negociação(Trade), ou definir o dinheiro GP que deseja pagar por uma compra, em seguida pressione o botão [Propor Negócio].";
	mes "Depois que ambas as partes propuseram seu lado, você pode tanto revisar a negociação, quanto aceitar ou rejeitar, fechando a janela de Negociação(Trade).\"";
    next;
    mes "[Negociante]";
	mes "\"Itens adicionados à janela de Negociação(trade) não podem ser removidos, portanto, os erros devem ser resolvidos mediante a anulação do negócio fechando a janela de Negociação(trade).";
	mes "Você precisa pressionar o botão de [Mudar] ou [Change] para deixar a outra pessoa saber quando de dinheiro em GP você está oferecendo pelos itens dela.\"";
    next;
    goto L_Later;

L_Later:
    mes "[Negociante]";
    mes "\"Por favor, sinta-se livre para veir mais tarde. Estou esperando um grande carregamento de mercadorias para o comércio.\"";
    goto L_Close;

L_Trade:
    mes "[Negociante]";
    mes "\"Bem vindo de volta! Meu embarque chegou e eu tenho um monte de coisas que eu posso negociar agora. O que você gostaria?\"";
    goto L_Trademenu;

L_Trademenu:
    menu
        "Você tem Pó de Ferro?", L_Iron,
        "Que tal Pó de Enxofre?", L_Sulphur,
        "Eu preciso de Poções Médias de Cura.", L_Healpots,
        "Eu acho que tenho tudo oque preciso, obrigado.", L_Close;

L_Iron:
    mes "[Negociante]";
    mes "\"Eu tenho alguns deles. Eu posso te dar quatro deles por 1 Minério de Ferro e " + @Ironprice + "gp.\"";
    menu
        "Ok.", L_Ipowder,
        "Que roubo! Não mesmo!", L_Close;

L_Sulphur:
    mes "[Negociante]";
    mes "\"Eu posso negociar 5 com você por 1 Pilha de Cinzas e " + @Sulphurprice + "gp.\"";
    menu
        "Aqui está.", L_Spowder,
        "Tá louco? Esqueçe!", L_Close;

L_Healpots:
    mes "[Negociante]";
    mes "\"Poções de cura são sempre úteis, mas eu raramente preciso de maiores que as pequenas. Eu ficaria feliz em trocar com você uma Poção Média de Cura por 3 Pequenas Poções de Cura e " + @Potionprice + "gp.\"";
    menu
        "Tudo bem.", L_Pots,
        "Nossa, está muito caro!.", L_Close;

L_Ipowder:
    if (countitem("IronOre") < 1) goto L_Missing;
    if (Zeny < @Ironprice) goto L_NotEnoughMoney;
    getinventorylist;
    if (@inventorylist_count == 100 && countitem("IronOre") > 1 && countitem("IronPowder") < 1) goto L_Full;
    delitem "IronOre", 1;
    set Zeny, Zeny - @Ironprice;
    getitem "IronPowder", 4;
    mes "[Negociante]";
    mes "\"Aqui está. Quer negociar mais alguma coisa?\"";
    menu
        "Sim.", L_Trademenu,
        "Não.", L_No;

L_Spowder:
    if (countitem("PileOfAsh") < 1) goto L_Missing;
    if (Zeny < @Sulphurprice) goto L_NotEnoughMoney;
    getinventorylist;
    if (@inventorylist_count == 100 && countitem("PileOfAsh") > 1 && countitem("SulphurPowder") < 1) goto L_Full;
    delitem "PileOfAsh", 1;
    set Zeny, Zeny - @Sulphurprice;
    getitem "SulphurPowder", 5;
    mes "[Negociante]";
    mes "\"Aqui está. Quer negociar mais alguma coisa?\"";
    menu
        "Sim.", L_Trademenu,
        "Não.", L_No;

L_Pots:
    if (countitem("SmallHealingPotion") < 3) goto L_Missing;
    if (Zeny < @Potionprice) goto L_NotEnoughMoney;
    getinventorylist;
    if (@inventorylist_count == 100 && countitem("SmallHealingPotion") > 3 && countitem("MediumHealingPotion") < 1) goto L_Full;
    delitem "SmallHealingPotion", 3;
    set Zeny, Zeny - @Potionprice;
    getitem "MediumHealingPotion", 1;
    mes "[Negociante]";
    mes "\"Aqui está. Quer negociar mais alguma coisa?\"";
    menu
        "Sim.", L_Trademenu,
        "Não.", L_No;

L_Full:
    mes "[Negociante]";
    mes "\"Você certamente fez muitas negociações... sua mochila está completamente cheia!  Volte quando tiver algum espaço.\"";
    goto L_Close;

L_Missing:
    mes "[Negociante]";
    mes "\"Parece que você esqueceu alguns items. Volte quando você tiver tudo oque foi combinado na negociação.\"";
    goto L_Close;

L_NotEnoughMoney:
    mes "[Negociante]";
    mes "\"Você não tem todo o dinheiro.\"";
    goto L_Close;

L_No:
    mes "[Negociante]";
    mes "\"Tenha um bom dia!\"";
    goto L_Close;

L_Close:
    set @Ironprice, 0;
    set @Sulphurprice, 0;
    set @Potionprice, 0;
    close;
}
