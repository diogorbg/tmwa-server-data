// Variables used: nibble 2 of QUEST_SouthTulimshar

001-1.gat,135,42,0|script|Vincent|113,
{
    // This NPC previously used the variable TMW_Quest
    callfunc "ClearVarTMW_Quest";

    set @state, ((QUEST_SouthTulimshar & NIBBLE_2_MASK) >> NIBBLE_2_SHIFT);

    if (@state >= 2) goto L_Done;
    if (@state == 1) goto L_Progress;
    goto L_Convince_Vincent_First;

L_Convince_Vincent_First:
    mes "[Vincent]";
    mes "\"Eu estou fazendo uma estatueta de ação. Estou quase acabando.\"";
    next;

    set @TEMP, rand(4);
    if(@TEMP == 1) goto L_Opening2;
    if(@TEMP == 2) goto L_Opening3;
    if(@TEMP == 3) goto L_Opening4;
    goto L_Opening1;

L_Opening1:
    mes "\"Eu preciso me mais apenas 10 Patas de Insetos para termina-la!\"";
    next;
    goto L_Ask;

L_Opening2:
    mes "\"Essa estatueta de um verme é incrível! Eu apenas preciso de 10 Patas de Insetos.\"";
    next;
    goto L_Ask;

L_Opening3:
    mes "\"Essa é uma boa estatueta de ação!Tudo oque preciso são algumas patas...\"";
    next;
    goto L_Ask;

L_Opening4:
    mes "\"Vcoê pode me trazer 10 Patas de Insetos? Eu preciso para terminar algumas partes da estatueta.\"";
    next;
    goto L_Ask;

L_Ask:
    mes "\"Você pode me ajudar com 10 Patas de Insetos?\"";
    next;
    menu
        "Sim.", L_Sure,
        "Não.", L_Close;

L_Sure:
    set @state, 1;
    callsub S_Update_Var;
    set @TEMP, rand(4);
    if(@TEMP == 1) goto L_Req2;
    if(@TEMP == 2) goto L_Req3;
    if(@TEMP == 3) goto L_Req4;
    goto L_Req1;

L_Req1:
    mes "[Vincent]";
    mes "\"Obrigado!\"";
    next;
    goto L_Wait;

L_Req2:
    mes "[Vincent]";
    mes "\"Não sei como lhe agradecer!\"";
    next;
    goto L_Wait;

L_Req3:
    mes "[Vincent]";
    mes "\"Eu vou lhe agradecer quando vocer trouxe-las!\"";
    next;
    goto L_Wait;

L_Req4:
    mes "[Vincent]";
    mes "\"Eu vou lhe dar uma pequena recompensa.\"";
    next;
    goto L_Wait;

L_Wait:
    mes "\"Agora por favor me traga as 10 Patas e Inseto.\"";
    goto L_Close;

L_Progress:
    if(countitem("BugLeg") >= 10)
        goto L_Have;
    mes "[Vincent]";
    mes "\"Pro favor me ajude a coletar 10 Patas de Insetos! Eu preciso delas para completar minha estatueta.\"";
    goto L_Close;

L_Have:
    mes "[Vincent]";
    mes "\"Exelente! finalmente vou poder terminar o modelo!!\"";
    next;
    if(countitem("BugLeg") < 10)
        goto L_Progress;
    delitem "BugLeg", 10;
    set Zeny, Zeny + 1000;
    set @state, 2;
    callsub S_Update_Var;

    mes "[Vincent]";
    mes "\"Aqui está, um pouco da minha arte!\"";
    mes "";
    mes "[1000 gold]";
    goto L_Close;

L_Done:
    mes "[Vincent]";
    mes "\"Obrigado pela ajuda!\"";
    goto L_Close;

L_Close:
    set @TEMP, 0;
    set @state, 0;
    close;

S_Update_Var:
    set QUEST_SouthTulimshar, (QUEST_SouthTulimshar & ~(NIBBLE_2_MASK) | (@state << NIBBLE_2_SHIFT));
    return;
}
