// Variables used: nibble 0 of QUEST_SouthTulimshar

001-1.gat,54,43,0|script|Mikhail|120,
{
    // This NPC previously used the variable TMW_Quest
    callfunc "ClearVarTMW_Quest";

    set @state, ((QUEST_SouthTulimshar & NIBBLE_0_MASK) >> NIBBLE_0_SHIFT);

    if (@state >= 6) goto L_Done;
    if (@state == 5) goto L_Progress;
    if (@state == 4) goto L_Start;
    goto L_Convince_Mikhail_First;

L_Convince_Mikhail_First:
    mes "[Mikhail]";
    mes "\"Olá...\"";
    mes "";
    mes "Parece que o rapaz gostaria de dizer mais, mas não se atreve a se abrir para você. Talvez você deve ajudar alguém em primeiro lugar, de modo que ele vê que suas intenções são boas.";
    goto L_Close;

L_Start:
    mes "[Mikhail]";
    mes "\"Minha avó me mandou para obter cinco Gosmas de Verme dos vermes no deserto, mas eles me assustam!\"";
    next;
    mes "\"Você parece uma boa pessoa. Você poderia pega-los para mim?\"";
    menu
        "Claro, eu vou levá-los para você.", L_accept,
        "Tenho outras coisas para fazer agora.", L_Close;

L_accept:
    mes "[Mikhail]";
    mes "\"Muito obrigado! Eu vou esperar por você aqui.\"";
    set @state, 5;
    callsub S_Update_Var;
    close;

L_Progress:
    mes "[Mikhail]";
    mes "\"Você me trouxe as cinco Gosmas de Verme que preciso?\"";
    next;
    menu
        "Sim, eles estão aqui, garoto!", L_try,
        "Ainda não, mas eu vou trazer em breve.", L_Close;

L_try:
    if (countitem("MaggotSlime") >= 5)
        goto L_get;
    mes "[Mikhail]";
    mes "\"Não me parece que você tem todos...\"";
    goto L_Close;

L_get:
    delitem "MaggotSlime", 5;
    getexp 100, 0;
    set @state, 6;
    callsub S_Update_Var;
    mes "[Mikhail]";
    mes "\"Ooh! Muito obrigado! Eu posso leva-los para a minha avó agora!\"";
    goto L_Close;

L_Done:
    mes "[Mikhail]";
    mes "\"Mais uma vez obrigado por me ajudar a obter essas Gosmas de Verme!\"";
    goto L_Close;

L_Close:
    set @state, 0;
    close;

S_Update_Var:
    set QUEST_SouthTulimshar, (QUEST_SouthTulimshar & ~(NIBBLE_0_MASK) | (@state << NIBBLE_0_SHIFT));
    return;
}
