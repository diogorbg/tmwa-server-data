//
// traduzido por Italo Luna


001-1.gat,108,55,0|script|Luca|102,
{
    set @EXP_BRAWLING, 2000;

    if (BaseLevel >= 35) goto L_teach;

    if (BaseLevel >= 25) goto L_teach_soon;

    mes "[Luca o Caçador]";
    mes "\"Ouch! Dói, esta ferida que eu tenho de batalhas.\"";
    next;
    menu "Diga-me sobre isso",L_Exp,"Não importa",L_Nev;

L_Exp:
    mes "[Luca o Caçador]";
    mes "\"Eu estava voltando de uma longa viagem. Corri para um grupo de escorpiões e começou a combatê-los para a experiência.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Então, de uma caverna veio um preto! Eu nunca tinha visto isso antes!\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Felizmente eu tinha uma câmera comigo! Aqui está uma foto dele... Deixe-me encontrá-lo, eu coloquei no bolso em algum lugar...\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Nossa! Meus bolsos foram rasgados\"";
    goto L_Close;

L_Nev:
    mes "[Luca o Caçador]";
    mes "\"Hmpf!\"";
    goto L_Close;

L_teach_soon:
    mes "[Luca o Caçador]";
    mes "\"Você cresceu um pouco mais forte desde que eu vi pela primeira vez, dar uma volta em torno da cidade assim! você sabe, se você treinar um pouco mais, talvez possamos ajudar uns aos outros um pouco?\"";
    goto L_Close;

L_teach:
    if (getskilllv(SKILL_POOL)) goto L_teachmore;

    mes "[Luca o Caçador]";
    mes "\"Olá! Você se tornou bastante aventureiro, não é mesmo? Eu acho que é hora de que alguém lhe ensinar algumas habilidades básicas.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Se você quiser, eu posso ajudar com isso!\"";
    next;
    menu
        "Claro, isso parece divertido!", L_Next,
        "Eu não estou interessado.", L_Nev;

L_Next:
    mes "[Luca o Caçador]";
    mes "\"Tudo certo! Não é tão fácil, apesar de. Primeiro você tem que aprender a se concentrar. Isso porque você pode aprender muitas habilidades, mas você não pode se concentrar em todos eles o tempo todo.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Quando você pode fazer isso, você pode aprender algumas verdadeiras habilidades, e quando você conhecê-los, você pode voltar para me dizer qual deles você deseja focar. negócio fechado?\"";
    next;
    menu
        "Sim!", L_teach0_follow,
        "Com certeza!", L_teach0_follow,
        "Espere ... o que você quer dizer?", L_Explain,
        "Por favor, explique um pouco mais.", L_Explain,
        "Não.", L_Nev;

L_teach0_follow:
    if (getskilllv(SKILL_POOL)) goto L_teachmore;

    mes "[Luca o Caçador]";
    mes "\"Ótimo! Agora, antes que eu possa dar-lhe essa habilidade, Tenho a certeza de que essas histórias sobre você é tudo verdade e que você realmente é experiente o suficiente.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Então eu vou lhe fazer algumas perguntas fáceis.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Primeiro, quem é o homem que guarda a entrada para o antigo cenário subterrâneo Tulimshar?\"";
    next;
    input @answer$;
    if (@answer$ != "Phaet" && @answer$ != "phaet") goto L_wronganswer;
    mes "[Luca o Caçador]";
    mes "\"Boa. Em seguida, o que é o nome do pipe-fumante que vive em cima de Lore Montanha e torna os produtos de couro lá?\"";
    next;
    input @answer$;
    if (@answer$ != "Pachua" && @answer$ != "pachua") goto L_wronganswer;
    mes "[Luca o Caçador]";
    mes "\"Ótimo! Mais um: Quem foi aventureiro que construiu Cove de Dimond para Dimond?\"";
    next;
    input @answer$;
    if (@answer$ != "Merlin" && @answer$ != "merlin") goto L_wronganswer;
    mes "[Luca o Caçador]";
    mes "Luca ri na emoção.";
    mes "\"Tudo certo! Você é a coisa real, meu amigo!\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Agora fique por lá. Isto deve levar apenas um segundo...\"";
    mes "Ele tira um pingente que ele está usando e mantém-se.";
    next;
    mes "[Luca o Caçador]";
    mes "\"Veja que a presa no final da cadeia? Mantenha olhando para ele.\"";
    next;
    mes "[Luca o Caçador]";
    mes "Ele começa a balançar o pingente para trás e para frente.";
    mes "\"E faça o que fizer, não pisque. Não desvie o olhar, e não pisque.\"";
    next;
    mes "[Luca o Caçador]";
    mes "O mundo vai ficando mais lento em torno de você...";
    next;
    mes "e para a frente...";
    next;
    mes "e para trás...";
    next;
    mes "e para a frente...";
    next;
    mes "e para trás...";
    next;
    mes "e...";
    next;
    mes "Você se sente relaxado.";
    next;
    mes "[Luca o Caçador]";
    mes "\"... ainda há?\"";
    mes "Você ouve o som de dedos estalando. Por que alguém iria fazer isso num dia tão calmo?";
    next;
    mes "[Luca o Caçador]";
    mes "\"Ah, bom ... Eu acho que funcionou. bom trabalho!\"";
    mes "ele sorri.";
    mes "[You gain 10,000 experience points]";
    mes "[You learned Skill Focus]";
    setskill SKILL_POOL, 1;
    getexp 10000, 0;
    next;
    goto L_teachmore2;

L_wronganswer:
    mes "[Luca o Caçador]";
    mes "\"Não, isso estar errado. Acho que você não está tão experiente como eu pensei que você estaria.\"";
    goto L_Close;

L_Explain:
    mes "[Luca o Caçador]";
    mes "\"Tudo bem, o que você quer saber?\"";
    next;
    goto L_Explain_loop;

L_Explain_loop:
    menu
        "Qual é o foco de habilidade?", L_explain_focus,
        "Quais habilidades existem?", L_explain_skills,
        "Como fazer o trabalho de habilidades?", L_explain_work,
        "Obrigado, eu acho que eu entendi!", L_Next;

L_explain_focus:
    mes "[Luca o Caçador]";
    mes "\"Bem, você pode aprender muitas habilidades. Mas você não pode realmente usar todos eles de uma só vez, nem mesmo velho Auldsbels' cabeça é grande o suficiente para isso! Então você tem que se concentrar.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Mas você pode mudar esse foco sempre que quiser. Funciona assim: Você vem para mim, você me diz o que você quer se concentrar, e fazemos os exercícios até que esteja preso na sua cabeça.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Se não houver espaço suficiente em sua cabeça, primeiro você tem que parar de pensar em outra coisas. Há um exercício que aprendi a fazer isso, Então, quando você falar comigo você pode simplesmente me deixar saber o que você quer esquecer.\"";
    next;
    mes "[Luca o Caçador]";
    if (getskilllv(SKILL_POOL) == 0)
        mes "\"Neste momento, você não pode se concentrar em nada ainda, então eu vou primeiro ter que te ensinar uma habilidade simples para manter seu foco.\"";
    if (getskilllv(SKILL_POOL) == 1)
        mes "\"Agora você só pode se concentrar em uma coisa de cada vez. Então você quer mudar o seu foco é, você tem que esquecer tudo aquilo que você já estar focado.\"";
    if (getskilllv(SKILL_POOL) > 1)
        mes "\"Você pode se concentrar em" + getskilllv(SKILL_POOL) + " habilidades agora mesmo.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Você pode voltar para se concentrar ou desfocar uma habilidade sempre que quiser, Estou aqui todos os dias.\"";
    next;
    mes "\"Para desfocagem você precisa trazer uma poção; uma poção especial. Essa poção não é o mais fácil de obter, mas eu tenho certeza que você é capaz.\"";
    next;
    goto L_Explain_loop;

L_explain_skills:
    mes "[Luca o Caçador]";
    mes "\"Bem, eu realmente não sei... Eu posso te ensinar brigando, o que é bom quando você correr para fora das flechas ou não tiver uma arma em torno de você .\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Mas existe outras habilidades em torno de. Alguns magos precisa de habilidades especiais para se concentrar, por isso pedimos em torno naquela multidão, se isso é coisa sua.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Eu também ouvi que existe alguém em Hurnscald que pode ajudá-lo a tornar o seu corpo mais resistente contra certas coisas. Mas o corpo e a mente pertencem um ao outro, então você provavelmente tem que se concentrar para que também.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Fora isso, é só pedir em torno! Tem que haver algumas pessoas que podem te ensinar algo...\"";
    next;
    goto L_Explain_loop;

L_explain_work:
    mes "[Luca o Caçador]";
    mes "\"Bem, habilidades permitem que você seja melhor em algumas coisas, ou permitir fazer o que você não poderia fazer sem eles. Quando você aprendeu uma habilidade e estão se concentrando sobre ele, então isso afeta o que você faz.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Agora quando você começa com uma habilidade, você não vai ser muito bom no que faz ainda. Mas se você continuar praticando você vai aprender como obter o melhor.\"";
    next;
    mes "[Luca o Caçador]";
    mes "\"Meu velho professor sempre chama assim recebendo 'pontos de habilidade' e que você pode vê-los com [F5], quem quer que seja.\"";
    next;
    //TODO:
    //mes "[Luca o Caçador]";
    //mes "\"She also said that with this [F5] thing you can get better at those skills, and that it costs you as many skill points as the level you want to go to.\"";
    //next;
    mes "[Luca o Caçador]";
    mes "\"Ela sempre teria uma poção cactos demais... mas ela era uma professora incrível.\"";
    mes "Ele acena com empolgação.";
    next;
    goto L_Explain_loop;

L_teachmore:
    mes "[Luca o Caçador]";
    mes "\"Que bom te ver de novo!\"";
    next;
    goto L_teachmore2;

L_teachmore2:
    setarray @choices, 0,0,0,0,0;
    setarray @choice$, "", "", "", "", "";
    set @choice_nr, 0;

    set @C_focus, 1;
    set @C_unfocus, 2;
    set @C_teachbrawling, 3;
    set @C_teachnothing, 4;
    set @C_explainagain, 5;
    set @C_nvm, 6;
    set @C_focusoverview, 7;

    getactivatedpoolskilllist;
    if (!((getskilllv(SKILL_POOL) - @skilllist_count) > 0)) goto L_teachmore2_nof;
    getunactivatedpoolskilllist;
    if (@skilllist_count == 0) goto L_teachmore2_nof;
    set @choices[@choice_nr], @C_focus;
    set @choice$[@choice_nr], "Gostaria de concentrar.";
    set @choice_nr, @choice_nr + 1;
    goto L_teachmore2_nof;

L_teachmore2_nof:
    getactivatedpoolskilllist;
    if (@skilllist_count == 0) goto L_teachmore2_nounf;
    set @choices[@choice_nr], @C_unfocus;
    set @choice$[@choice_nr], "Gostaria de desfocar.";
    set @choice_nr, @choice_nr + 1;
    goto L_teachmore2_nounf;

L_teachmore2_nounf:
    if (!(getskilllv(SKILL_BRAWLING)))
        set @choices[@choice_nr], @C_teachbrawling;
    if (getskilllv(SKILL_BRAWLING))
        set @choices[@choice_nr], @C_teachnothing;
    set @choice$[@choice_nr], "Você pode me ensinar uma habilidade?";
    set @choice_nr, @choice_nr + 1;

    set @choices[@choice_nr], @C_explainagain;
    set @choice$[@choice_nr], "Can you explain skills again?";
    set @choice_nr, @choice_nr + 1;

    if (!(getskilllv(SKILL_POOL))) goto L_teachmore2_noovv;
    set @choices[@choice_nr], @C_focusoverview;
    set @choice$[@choice_nr], "Can you tell me what skills I'm currently focused on?";
    set @choice_nr, @choice_nr + 1;
    goto L_teachmore2_noovv;

L_teachmore2_noovv:
    set @choices[@choice_nr], @C_nvm;
    set @choice$[@choice_nr], "Eu estou pronto agora, grato!";
    set @choice_nr, @choice_nr + 1;

    menu
        @choice$[0],L_MenuItems,
        @choice$[1],L_MenuItems,
        @choice$[2],L_MenuItems,
        @choice$[3],L_MenuItems,
        @choice$[4],L_MenuItems;

L_MenuItems:
    set @menu, @menu - 1;
    if (@choices[@menu] == 0) goto L_Close;
    if (@choices[@menu] == @C_focus) goto L_focus;
    if (@choices[@menu] == @C_unfocus) goto L_unfocus;
    if (@choices[@menu] == @C_teachbrawling) goto L_teach_brawling;
    if (@choices[@menu] == @C_teachnothing) goto L_teach_nothing;
    if (@choices[@menu] == @C_explainagain) goto L_teachmore_explain;
    if (@choices[@menu] == @C_focusoverview) goto L_focus_overview;
    if (@choices[@menu] == @C_nvm) goto L_Close;
    goto L_Close;

L_teachmore_explain:
    goto L_Explain;

L_focus:
    getunactivatedpoolskilllist;
    set @skilllist_id[@skilllist_count], 0;
    set @skilllist_name$[@skilllist_count], "Nevermind";
    set @skilllist_name$[@skilllist_count + 1], "";
    menu
        @skilllist_name$[0], L_MenuItems1,
        @skilllist_name$[1], L_MenuItems1,
        @skilllist_name$[2], L_MenuItems1,
        @skilllist_name$[3], L_MenuItems1,
        @skilllist_name$[4], L_MenuItems1,
        @skilllist_name$[5], L_MenuItems1,
        @skilllist_name$[6], L_MenuItems1,
        @skilllist_name$[7], L_MenuItems1;

L_MenuItems1:
    set @menu, @menu - 1;
    if (@skilllist_id[@menu] == 0) goto L_teachmore2;

    poolskill @skilllist_id[@menu];
    goto L_teachmore2;

L_unfocus:
    if (countitem("GrimaceOfDementia") > 0) goto L_unfocus_menu;

    if (MAGIG_FLAGS & MFLAG_KNOWS_UNFOCUS_RECIPE) goto L_nopotion;

    mes "[Luca o Caçador]";
    mes "\"Desfocagem é uma das partes mais duras, porque ele está afundado. Você vê?\"";
    next;
    mes "\"Então, para remover esse foco mental vamos precisar de uma poção especial.\"";
    next;
    mes "\"Talvez você deve consultar alguns alquimistas muito habilidosos.\"";
    next;
    mes "\"Ah sim, é claro que você vai precisar de uma receita. Embora o alquimista normalmente sabe disso.\"";
    next;
    mes "\"A poção é chamado Careta da Demência. Assim, o alquimista vai saber com certeza.\"";
    set MAGIC_FLAGS, MAGIC_FLAGS | MFLAG_KNOWS_UNFOCUS_RECIPE;
    menu
        "Ok, vou conseguir uma poção de careta de Demência", L_Close,
        "Tudo bem, volto já!", L_Close;

L_nopotion:
    mes "[Luca o Caçador]";
    mes "\"Ei, você realmente precisa da poção demência desfocagem.\"";
    next;
    mes "\"Volte quando você encontrar um alquimista adequado que pode misturar essa poção.\"";
    next;
    goto L_teachmore2;

L_unfocus_menu:
    mes "[Luca o Caçador]";
    mes "\"Ahh, eu vejo que você tem uma poção demência.\"";
    next;
    mes "\"Que habilidade você gostaria de desfocar?\"";

    getactivatedpoolskilllist;

    set @skilllist_id[@skilllist_count], 0;
    set @skilllist_name$[@skilllist_count], "Nevermind";
    set @skilllist_name$[@skilllist_count + 1], "";
    menu
        @skilllist_name$[0], L_MenuItems2,
        @skilllist_name$[1], L_MenuItems2,
        @skilllist_name$[2], L_MenuItems2,
        @skilllist_name$[3], L_MenuItems2,
        @skilllist_name$[4], L_MenuItems2,
        @skilllist_name$[5], L_MenuItems2,
        @skilllist_name$[6], L_MenuItems2,
        @skilllist_name$[7], L_MenuItems2;

L_MenuItems2:
    set @menu, @menu - 1;
    if (@skilllist_id[@menu] == 0) goto L_teachmore2;

    if (countitem("GrimaceOfDementia") == 0) goto L_nopotion;
    delitem "GrimaceOfDementia", 1;

    unpoolskill @skilllist_id[@menu];
    goto L_Close;

L_focus_overview:
    getactivatedpoolskilllist;

    if (@skilllist_count != 0) goto L_focus_overview1;
    mes "[Luca o Caçador]";
    mes "\"Você não está focada em uma habilidade no momento.\"";
    next;
    goto L_teachmore2;

L_focus_overview1:
    mes "[Luca o Caçador]";
    set @message$, "\"No momento você está focado em ";
    goto L_focus_overview2;

L_focus_overview2:
    set @skilllist_count, @skilllist_count-1;
    set @message$, @message$ + @skilllist_name$[@skilllist_count];
    if (@skilllist_count == 0) goto L_focus_overview3;
    set @message$, @message$ + ", ";
    goto L_focus_overview2;

L_focus_overview3:
    set @message$, @message$ + ".\"";
    mes @message$;
    next;
    goto L_teachmore2;

L_teach_brawling:
    mes "[Luca o Caçador]";
    mes "\"Bem, sim, eu posso te ensinar brigando. É uma coisa boa, se você não tem mais nada, mas suas mãos para se defender.\"";
    next;
    mes "Ele está rindo.";
    mes "\"E é útil, se algum moço começa a ficar desobediente.\"";
    next;
    mes "Ele levanta as mãos na frente de seu rosto e fecha os punhos.";
    mes "\"Ok, vamos lá! Mantenha as mãos assim.\"";
    next;
    mes ".......";
    next;
    mes "Alguns machucados depois.";
    mes "\"Ótimo! Eu acho que, você conseguiu.\"";
    next;
    set @SUP_id, SKILL_BRAWLING;
    set @SUP_lvl, 1;
    set @SUP_name$, "Brawling";
    set @SUP_xp, @EXP_BRAWLING;
    callfunc "SkillUp";
    goto L_Close;

L_teach_nothing:
    mes "[Luca o Caçador]";
    mes "\"Eu sou capaz de ensiná-lo a brigar. Para as outras profissões você deve visitar outras pessoas.\"";
    next;
    mes "\"Você vê, eu sou um aventureiro. Todo mundo tem seus truques. Mas você realmente deve visitar alguém que é um professor, não um aventureiro.\"";
    next;
    goto L_teachmore2;

L_Close:
    set @SUP_id, 0;
    set @SUP_lvl, 0;
    set @SUP_name$, "";
    set @SUP_xp, 0;
    set @EXP_BRAWLING, 0;
    set @answer$, "";
    set @message$, "";
    cleararray @choices[0], 0, 4;
    cleararray @choice$[0], "", 4;
    set @choice_nr, 0;
    set @C_focus, 0;
    set @C_unfocus, 0;
    set @C_teachbrawling, 0;
    set @C_teachnothing, 0;
    set @C_explainagain, 0;
    set @C_nvm, 0;
    set @C_focusoverview, 0;
    close;
}
