//

function|script|ElanoreFix|,
{
    set @Q_MASK, NIBBLE_1_MASK;
    set @Q_SHIFT, NIBBLE_1_SHIFT;

    set @Q_STATUS_MAX, 3; // FIXME once level 3 magic was released

    set @Q_status, (QUEST_MAGIC2 & @Q_MASK) >> @Q_SHIFT;
    if (@Q_status > @Q_STATUS_MAX)
    set @Q_status, @STATUS_INITIAL;
    if (@Q_status > 1 && (getskilllv(SKILL_MAGIC_LIFE) < 2))
    set @Q_status, @STATUS_INITIAL;

    set QUEST_MAGIC2, (QUEST_MAGIC2 & ~(@Q_MASK) | (@Q_status << @Q_SHIFT));
    return;
}

001-1.gat,58,68,0|script|Elanore#_M|108,
{
    set @has_magic, getskilllv(SKILL_MAGIC);

    set @Q_MASK, NIBBLE_1_MASK;
    set @Q_SHIFT, NIBBLE_1_SHIFT;

    set @Q_status, (QUEST_MAGIC2 & @Q_MASK) >> @Q_SHIFT;
    set @Q_heal_exp, MAGIC_EXPERIENCE >> 24;

    // Set up SkillUp function
    set @SUP_id, SKILL_MAGIC_LIFE;
    set @SUP_name$, "Life Magic";

    set @STATUS_INITIAL, 0;
    set @STATUS_LEARNED_LIGHT_HEAL, 1;
    set @STATUS_MASTERED_LIGHT_HEAL, 2;
    set @STATUS_LEARNED_LAY_ON_HANDS, 3;
    set @STATUS_WAITING_FOR_KADIYA, 4;
    set @STATUS_READY_TO_LEARN_CURE_POISON, 5;
    set @STATUS_LEARNED_CURE_POISON, 6;

    callfunc "ElanoreFix";

    // This operation works around an earlier possible corruption of this state
    if (@Q_status > @Q_STATUS_MAX)
    set @Q_status, @STATUS_INITIAL;
    callsub S_update_var;
    if (sc_check(sc_poison)) goto L_CurePoison;
    if (BaseLevel > 10) goto L_NoHeal;
    set @TEMP,rand(4);
    if(@TEMP == 1) goto L_Heal2;
    if(@TEMP == 2) goto L_Heal3;
    if(@TEMP == 3) goto L_Heal4;
    goto L_Heal1;

L_Heal1:

    mes "[Elanore the Healer]";
    mes "\"Você não me parece muito bem... Deixe-me cuidar de você.\"";
    next;
    goto L_Heal_L;

L_Heal2:

    mes "[Elanore the Healer]";
    mes "\"Deixe eu cuidar desses seus ferimentos.\"";
    next;
    goto L_Heal_L;

L_Heal3:

    mes "[Elanore the Healer]";
    mes "\"Precisando de cura?\"";
    next;
    goto L_Heal_L;

L_Heal4:

    mes "[Elanore the Healer]";
    mes "\"As vezes você só precisa correr das lutas.\"";
    next;
    goto L_Heal_L;

L_Heal_L:
    set @TEMP,rand(3);
    if(@TEMP == 1) goto L_Heal_2;
    if(@TEMP == 2) goto L_Heal_3;
    if(@TEMP == 3) goto L_Heal_4;
    goto L_Heal_1;

L_Heal_1:

    mes "[Elanore the Healer]";
    mes "\"Aqui vamos nós!\"";
    heal 10000,10000;
    goto L_Close;

L_Heal_2:

    mes "[Elanore the Healer]";
    mes "\"Nem doeu! Certo?";
    heal 10000,10000;
    goto L_Close;

L_Heal_3:

    mes "[Elanore the Healer]";
    mes "\"Você precisa tomar mais cuidado!\"";
    heal 10000,10000;
    goto L_Close;

L_Heal_4:

    mes "[Elanore the Healer]";
    mes "\"Muito melhor, né?\"";
    heal 10000,10000;
    goto L_Close;

L_NoHeal:
    if (MAGIC_FLAGS)
        goto L_Chat;
    goto L_NoHealMessage;

L_NoHealMessage:
    mes "[Elanore the Healer]";
	mes "\"Me desuclpe mais você é muito novo para isso, eu não posso ajudar você, a não ser que seu level seja maior que dez.";
	mes "Muita gente está machucada por conta do terremoto, eu realmente não tenho tempo para lhe ajudar.";
	mes "Você pode descansar no bar aqui perto.\"";
    goto L_Close;

L_Chat:
    mes "[Elanore the Healer]";
    mes "\"Olá! Posso lhe ajudar?\"";
    next;
    goto L_Main;

L_Main:
    if (@has_magic && (@Q_status == @STATUS_INITIAL))
        menu
            "Poderia me curar?", L_NoHealMessage,
            "Pode me ensinar algo sobre magia?", L_Teach,
            "O que você sabe sobre...", L_Question,
            "Até mais!", L_bye;
    if (@has_magic && (@Q_status > @STATUS_INITIAL))
        menu
            "Poderia me curar?", L_NoHealMessage,
            "Pode me ensinar mais?", L_Teach,
            "Onde eu consigo mais Lifestones?", L_Lifestones,
            "O que você sabe sobre...", L_Question,
            "Até mais!", L_bye;
    if (!@has_magic)
        menu
            "Poderia me curar?", L_NoHealMessage,
            "O que você sabe sobre...", L_Question,
            "Até mais!", L_bye;
    goto L_Close;

OnPCKillEvent:
    if (attachrid(@killerrid) == 0)
        end;
    set @Q_heal_exp, MAGIC_EXPERIENCE >> 24;
    if (@Q_heal_exp < 8) goto L_OnPcKillWipe;
    set @Q_heal_exp, @Q_heal_exp - 8;
    goto L_OnPcKillFinish;

L_OnPcKillWipe:
    set @Q_heal_exp, 0;
    goto L_OnPcKillFinish;

L_OnPcKillFinish:
    set MAGIC_EXPERIENCE, (MAGIC_EXPERIENCE & ~(255 << 24)) | (@Q_heal_exp << 24);
    goto L_Close;

L_Lifestones:
    mes "[Elanore the Healer]";
    mes "\"Eu sempre preciso de alguns componentes para fazer minhas magias. Se me trouxer dez ervas gamboge, dez ervas alizarinas, dez ervas mauvas e dez ervas cobalto, Eu posso fazer dez lifestones; Te darei metade deles.\"";
	next;
    menu
        "OK, vou pegar.", L_Close,
        "Nunca!", L_Close,
        "Aqui estão!", L_Lifestones_Trade,
        "Eu consigo fazer sozinho?", L_Lifestones_MakeSelf;

L_Lifestones_Trade:
    if (countitem("MauveHerb") < 10) goto L_Lifestones_Trade_Missing;
    if (countitem("GambogeHerb") < 10) goto L_Lifestones_Trade_Missing;
    if (countitem("AlizarinHerb") < 10) goto L_Lifestones_Trade_Missing;
    if (countitem("CobaltHerb") < 10) goto L_Lifestones_Trade_Missing;
    getinventorylist;
    if (@inventorylist_count == 100 && countitem("Lifestone") == 0
        && countitem("MauveHerb") > 10
        && countitem("GambogeHerb") > 10
        && countitem("AlizarinHerb") > 10
        && countitem("CobaltHerb") > 10) goto L_Lifestones_NoRoom;

    delitem "MauveHerb", 10;
    delitem "GambogeHerb", 10;
    delitem "AlizarinHerb", 10;
    delitem "CobaltHerb", 10;
    getitem "Lifestone", 5;
    getexp 200, 1;
    mes "[Elanore the Healer]";
	mes "Elanore cuidadosamente pega suas folhas, então sorri.";
    mes "\"Isto me parece bom.\"";
    mes "Ela pega as quarenta folhas então preciona todas juntas em suas mãos, e susurra algo.";
    next;
    mes "[Elanore the Healer]";
    mes "Ela abre suas mãos de novo, e no lugar das folhas estão 10 cristais brilhantes.";
    mes "\"Cinco para você e cinco para mim.\"";
    mes "Ela sorri.";
    mes "[200 experience points]";
    goto L_Close;

L_Lifestones_NoRoom:
    mes "[Elanore the Healer]";
    mes "\"Oh... Me parece que você não tem lugar para os lifestones. Más volte mais tarde quando tiver mais espaço.\"";
    goto L_Close;

L_Lifestones_Trade_Missing:
    mes "[Elanore the Healer]";
    mes "Elanore conta as folhas que você deu a ela.";
    mes "\"Não, me desculpa. Por favor traga-me dez de cada gamboges, alizarinas, mauves, e cobaltas.\"";
    goto L_Close;

L_Lifestones_MakeSelf:
    mes "[Elanore the Healer]";
    if (@has_magic < 2) goto L_Lifestones_MakeSelf_no;
    goto L_Lifestones_MakeSelf_yes;

L_Lifestones_MakeSelf_yes:
    mes "\"Oh, mais que legal! Você está forte o suficiente para fazer seus próprios lifestones, usando o encantamento '" + getspellinvocation("enchant-lifestone") + "'. Isso ira consumir uma perna de inseto ou uma gosma verde ou uma de cada tipo de ervas.\"";
    goto L_Close;

L_Lifestones_MakeSelf_no:
    mes "\"Não. você ainda não me parece tão forte. Por favor volte e fale comigo de novo quando tiver mais controle sobre sua magia.\"";
    goto L_Close;

L_Teach:
    if (@Q_status == @STATUS_INITIAL) goto L_Teach_Initial;
    if (@Q_status == @STATUS_LEARNED_LIGHT_HEAL) goto L_Teach_CheckAdvanceTo2;
    if (@Q_status == @STATUS_MASTERED_LIGHT_HEAL) goto L_Teach_CheckAdvanceToLOH;
    if (@Q_status == @STATUS_LEARNED_LAY_ON_HANDS) goto L_Teach_Explain3;
    if (@Q_status == @STATUS_WAITING_FOR_KADIYA) goto L_Teach_CheckAdvanceTo3;
    if (@Q_status == @STATUS_READY_TO_LEARN_CURE_POISON) goto L_Teach_CurePosion;
    if (@Q_status == @STATUS_LEARNED_CURE_POISON) goto L_Teach_dunno;

    mes "[Elanore the Healer]";
    mes "\"Você tem tido um bom progresso, más seu poder magico avançará no futuro. Mas, eu gostaria de lhe observar um pouco mais para me certificar que será um bom curandeiro.\"";
    goto L_Close;

L_Teach_Initial:
    mes "[Elanore the Healer]";
    mes "\"Eu só poderei te ensinar a magia vinda da Escola da Cura. E o farei caso me prove que será um bom curandeiro - isto é, caso queira usar seu poder para ajudar os outros.\"";
    mes "Ela hesita.";
    next;
    mes "[Elanore the Healer]";
    mes "\"Eu não te conheço muito bem, mais eu acho que não tem problema nenhum te ensinar uma magia basica de cura.\"";
    mes "\"Ela entra em sua barraca e volta com um pequeno pedaço de cristal, reluzente ao sol.";
    mes "\"Aqui está um lifestone, energia vital num cristal. Você vai usar ele para sua magia de cura.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"Para curar alguém, primeiro ache a ferida. Como você é um iniciante, terá que toca-la. Segure o lifestone em uma mão, tocando a ferida com a outra.\"";
    mes "\"E então diga, '" + getspellinvocation("lesser-heal") + "', seguindo do nome de quem quer curar.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"Talvez você queira anotar isso em algum lugar. De fato, você talvez tenha que manter uma nota com todas as magias que escuta, nunca se sabe se vai as ouvir novamente!\"";
    mes "\"A conjuração é '" + getspellinvocation("lesser-heal") + "'.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"Essa só vai curar cortes e contusões, e ela leva certo tempo para fazer efeito. Ela não será útil para ossos quebrados ou feridas mais sérias!\"";
    mes "\"Para curar a si mesmo, só é necessario dizer '" + getspellinvocation("lesser-heal") + "'.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"Deixe-me dar um lifestone para você praticar.\"";
    next;
    getinventorylist;
    if ((@inventorylist_count == 100) && (countitem("Lifestone") == 0)) goto L_Teach_Initial_Noroom;
    getitem "Lifestone", 1;
    set @Q_status, @STATUS_LEARNED_LIGHT_HEAL;
    callsub S_update_var;

    mes "[Elanore the Healer]";
    mes "Elanore entrega o cristal a você.";
    mes "\"Vamos lá. Eu espero que você consiga completar com exito!\"";
    goto L_Close;

L_Teach_Initial_Noroom:
    mes "[Elanore the Healer]";
    mes "\"Oh... você não pode mais carregar coisinhas na sua bolsa, não é?  Certo, volte depois, e então e lhe darei isto.\"";
    goto L_Close;

L_Teach_CheckAdvanceTo2:
    mes "[Elanore the Healer]";
    if (@Q_heal_exp < 20) goto L_Teach_CheckAdvanceTo2_fail;
    mes "Elanore sorri para você.";
    mes "\"Pelo que tenho ouvido, você tem procurado fazer de tudo para virar um curandeiro. Eu decidi te aceitar como estudante da Escola da Magia da Cura.\"";
    next;
    if (getskilllv(SKILL_MAGIC) < 2) goto L_Teach_AdvanceTo2_warn;
    goto L_Teach_AdvanceTo2_skip;

L_Teach_AdvanceTo2_warn:
    mes "[Elanore the Healer]";
    mes "\"Más ja vou avisando: Você será incapaz de avançar com a magia de cura enquanto não avançar sua magia basica.\"";
    next;
    goto L_Teach_AdvanceTo2_skip;

L_Teach_AdvanceTo2_skip:
    mes "[Elanore the Healer]";
    mes "\"Por favor, fique parado.\"";
    mes "Ela caminha até você, mexe seus ombros, e sorri para você.";
    next;
    mes "[Elanore the Healer]";
    mes "Então, então ela fica na ponta dos pés e da um beijo em sua testa.";
    next;
    mes "[Elanore the Healer]";
    mes "Você se sente estranho, seu corpo começa a esquentar e a formigar.";
    set @Q_status, @STATUS_MASTERED_LIGHT_HEAL;
    callsub S_update_var;
    set @SUP_xp, 5000;
    set @SUP_lvl, 2;
    callfunc "SkillUp";
    goto L_Close;

L_Teach_CheckAdvanceTo2_fail:
    mes "\"Imagino que eu precise te observar um pouco mais para ver se você será um bom curandeiro.\"";
    if (@Q_heal_exp < 5)
        mes "\"Você ainda está no inicio do seu caminho, continue praticando.\"";
    if ((@Q_heal_exp >= 5) && (@Q_heal_exp < 15))
        mes "\"Você está fazendo progresso, porém, ainda precisa praticar um pouco mais.\"";
    if (@Q_heal_exp >= 15)
        mes "\"Você tem feito um bom progresso, más por favor continue com seus esforços.\"";
    goto L_Close;

L_Teach_CheckAdvanceToLOH:
    if (getskilllv(SKILL_MAGIC) < 2) goto L_Teach_LOH_advance_abort0;
    if (@Q_heal_exp < 30) goto L_Teach_LOH_advance_abort1;

    mes "[Elanore the Healer]";
    mes "\"I will now teach you how to heal by laying on your hands. The technique is similar to the spell I taught you at the beginning, but this time you transfer your own health instead of drawing health from a lifestone.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"First, lay your hand on the person you wish to heal. You needn't touch the injury itself, though you have to touch the skin until you are a little more experienced. Then, medidate on the word '" + getspellinvocation("lay-on-hands") + "'\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"This will let your own life force flow into the person you are healing. If you are badly injured yourself, you will not be able to do this.\"";
    mes "[1000 experience points]";
    getexp 1000, 0;
    set @Q_status, @STATUS_LEARNED_LAY_ON_HANDS;
    callsub S_update_var;
    goto L_Close;

L_Teach_LOH_advance_abort0:
    mes "[Elanore the Healer]";
    mes "\"You don't have enough magical power to learn the next healing spell yet.\"";
    goto L_Close;

L_Teach_LOH_advance_abort1:
    mes "[Elanore the Healer]";
    mes "\"I'm sorry, but I am not yet convinced that you are a good enough healer. Please continue in your endeavours.\"";
    goto L_Close;

L_Teach_Explain3:
    if (getskilllv(SKILL_MAGIC) < 3) goto L_Teach_Explain3_abort;
    if (@Q_heal_exp < 50) goto L_Teach_Explain3_fail;

    mes "[Elanore the Healer]";
    mes "Elanore smiles.";
    mes "\"You seem to be ready to advance as a healer. There is one favour I must ask of you first, though.\"";
    set QUEST_MAGIC2, (QUEST_MAGIC2 & ~(NIBBLE_3_MASK)); // Initialise Kadiya quest
    set @Q_status, @STATUS_WAITING_FOR_KADIYA;
    callsub S_update_var;
    next;

    mes "[Elanore the Healer]";
    mes "\"There is a man here in Tulimshar with a beautiful young daughter; his name is Omar, and his daughter's name is Kadiya.\"";
    mes "\"Once, when I was a young apprentice, I made a mistake treating Omar. Ever since, he no longer trusts me.\"";
    next;

    mes "[Elanore the Healer]";
    mes "\"But his daugher has contracted Mopox, so she needs help. He doesn't trust me, but perhaps he will trust you; you have been helping quite a few people, after all.\"";
    next;
    goto L_3_menu;

L_3_menu:
    menu
        "What should I do?", L_3_what_to_do,
        "Where can I find the sick girl?", L_3_where,
        "How did you mess up?", L_3_messup,
        "I will get started right away.", L_Main,
        "Maybe later.", L_Main;

L_3_what_to_do:
    mes "[Elanore the Healer]";
    mes "\"You will have to brew a Mopox cure potion and give it to her. You should ask Caul to help you, he knows the recipe.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"First, you will need a concentration potion and an iron potion – together, they will give her her strength back. You also need a lifestone, and a gamboge leaf against the toxins in her body.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"You then have to add in roots, water, mauve leaves and sulphur powder until the brew is gooey and neither light nor dark. Caul will explain the details to you.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"There is a transmutation spell to make sulphur powder. If you don't know the magic, perhaps you can ask a transmutation mage to help you with this part.\"";
    next;
    goto L_3_menu;

L_3_messup:
    mes "[Elanore the Healer]";
    mes "\"Ah... well, Omar is a trader, and a frequent traveler. One day he came back from a journey with fleas in his beard and asked me to get rid of them.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"Unfortunately I used the wrong lotion on the fleas, so instead of being scared away, they grew to enormous proportions and ate up all of his beard.\"";
    next;
    mes "[Elanore the Healer]";
    mes "Elanore blushes.";
    mes "\"Ever since then he hasn't been very trusting towards my skills, and I suppose that I can't blame him. But I don't want poor Kadiya to suffer because of that.\"";
    next;
    goto L_3_menu;

L_3_where:
    mes "[Elanore the Healer]";
    mes "\"Kadiya and Omar live relatively close by. See this corner in the inner city wall east of here?  Turn past it, then go north. It's the second house.\"";
    next;
    goto L_3_menu;

L_Teach_Explain3_abort:
    mes "[Elanore the Healer]";
    mes "\"I'm sorry, but you lack the magical power to advance.\"";
    goto L_Close;

L_Teach_Explain3_fail:
    mes "[Elanore the Healer]";
    if (@Q_heal_exp < 30)
        mes "\"You seem to have regressed as a healer. This is worrisome; please mend your ways or I will not teach you any further.\"";
    if ((@Q_heal_exp >= 30) && (@Q_heal_exp < 40))
        mes "\"You still need more practice in the art of healing.\"";
    if ((@Q_heal_exp >= 40))
        mes "\"You are doing well, but you need some more practice as a healer first.\"";
    goto L_Close;

L_Teach_CheckAdvanceTo3:
    callfunc "KadiyaSubquestConsts";

    if (@Q_kadiya_status >= @Q_STATUS_COMPLETED) goto L_Teach_AdvanceTo3Ready;

    mes "[Elanore the Healer]";
    mes "\"Please help Kadiya.\"";
    next;
    goto L_3_menu;

L_Teach_AdvanceTo3Ready:
    mes "[Elanore the Healer]";
    mes "\"I have heard of what you did. Hiding the potion in her favorite food – that was clever.\"";
    mes "She smiles at you.";
    mes "\"Omar let me have a look at her earlier, and she seems to be recovering. You did very well.";
    next;
    mes "[Elanore the Healer]";
    mes "Elanor turns towards you, rises up on her toes and kisses you on the forehead.";
    next;
    set @xp, 30000;
    if (@Q_kadiya_status == @Q_STATUS_COMPLETED_ELANORE)
        set @xp, 50000;
    mes "[Elanore the Healer]";
    mes "You feel a strange, tingling kind of warmth spread through your body.";
    mes "[" + @xp + " experience points]";
    mes "[Level 3 in Life Magic]";

    set @Q_status, @STATUS_READY_TO_LEARN_CURE_POISON;
    callsub S_update_var;
    getexp @xp, 0;
    if (getskilllv(SKILL_MAGIC_LIFE) < 3)
        setskill SKILL_MAGIC_LIFE, 3;
    next;

    if (@Q_kadiya_status != @Q_STATUS_COMPLETED_ELANORE) goto L_Main;

    mes "[Elanore the Healer]";
    mes "\"Overall I am impressed by your progress. If you continue as you are doing now, you might become a great healer.\"";
    mes "She hands you a handful of lifestones.";
    mes "\"Please come back soon; there is more that I would like to teach you.\"";
    next;
    set MAGIC_FLAGS, MAGIC_FLAGS | MFLAG_ELANORE_OMAR;
    getitem "Lifestone", 20;

    goto L_Main;

L_Teach_CurePosion:
    mes "[Elanore the Healer]";
    mes "\"Thank you again for your help with Kadiya!\"";
    mes "\"Curing sicknesses is often quite difficult; most of them require complicated potions to be brewed.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"Fortunately, things are much easier with most poisons. For those we have a simple spell that only requires a Gamboge leaf.\"";
    mes "\"This is the next spell that I would like to teach you.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"If you would like to cure someone who is poisoned, first rub a Gamboge leaf between your hands. Your hands must be covered in Gamboge liquid for this to work.\"";
    mes "\"Speak the invocation, `" + getspellinvocation("cure-poison") + "'.\"";
    mes "\"Next, you either touch the poisoned person with your hands, or speak their name. You have to be close for this to work, though.\"";
    next;
    mes "[Elanore the Healer]";
    mes "\"Once again, the invocation is `" + getspellinvocation("cure-poison") + "'.\"";
    mes "\"Come back again soon; there is another spell I would like to teach you.\"";
    next;
    set @Q_status, @STATUS_LEARNED_CURE_POISON;
    callsub S_update_var;
    goto L_Main;

L_Teach_dunno:
    mes "[Elanore the Healer]";
    mes "\"Hmm. I wanted to teach you something, but I've forgotten what it was... please come back later.\"";
    next;
    goto L_Main;

L_Question:
    callfunc "MagicTalkOptionsSetup";
    set @ignore, @QQ_ELANORE;
    callfunc "MagicTalkMenu";

    if (@c == 0) goto L_Main;
    if (@c == @QQ_MANASEED) goto L_Q_manaseed;
    if (@c == @QQ_MANAPOTION) goto L_Q_manapotion;
    if (@c == @QQ_WYARA) goto L_Q_wyara;
    if (@c == @QQ_SAGATHA) goto L_Q_sagatha;
    if (@c == @QQ_AULDSBEL) goto L_Q_auldsbel;
    if (@c == @QQ_OLDWIZ) goto L_Q_oldwiz;
    if (@c == @QQ_ASTRALSOUL) goto L_Q_astralsoul;

    mes "[Elanore the Healer]";
    mes "Elanore shakes her head.";
    mes "\"I am sorry, but I don't think that I know anything that would help you.\"";
    next;
    goto L_Main;

L_Q_astralsoul:
    mes "[Elanore the Healer]";
    mes "\"The astral soul skill is one of the most important skills for mages. I learned this focus long time ago. Without that skill your magic is quite useless, isn't it? If the rumors are really true, there is an experienced mage in the Woodlands around Hurnscald.\"";
    next;
    goto L_Main;

L_Q_oldwiz:
    mes "[Elanore the Healer]";
    mes "\"He is a kind old man. He stays mostly with his books and his apprentice, though we have chatted a few times.\"";
    next;
    goto L_Main;

L_Q_manaseed:
    mes "[Elanore the Healer]";
    mes "She smiles.";
    mes "\"If the rumors are true and there really is a Mana Seed, then that would be wonderful news – perhaps we will have a few more healers soon!\"";
    next;
    goto L_Main;

L_Q_manapotion:
    mes "[Elanore the Healer]";
    mes "\"I'm sorry, but I can't help you with that.\"";
    next;
    goto L_Main;

L_Q_wyara:
    mes "[Elanore the Healer]";
    mes "\"Wyara is the Hurnscald town witch. She was also the town healer before the Doctor moved there.\"";
    next;
    goto L_Main;

L_Q_sagatha:
    mes "[Elanore the Healer]";
    mes "\"Oh, I have only met Sagatha once or twice, while collecting herbs. She is a warm and gentle person, but she tries to hide it.\"";
    next;
    goto L_Main;

L_Q_auldsbel:
    mes "[Elanore the Healer]";
    mes "\"Oh, sorry – I don't really know him very well.\"";
    next;
    goto L_Main;

L_CurePoison:
    mes "[Elanore the Healer]";
    mes "\"Oh querido!  Isso me parece um veneno; Aguente...\"";
    mes "Ela faz um gesto complicado";
        sc_end(sc_poison);
        sc_end(sc_slowpoison);
    misceffect sfx_magic_life, strcharinfo(0);
    misceffect sfx_magic_life;
    next;
    mes "[Elanore the Healer]";
    mes "\"Pronto, novinho em folha!  Agora tomará mais cuidado, certo?\"";
    goto L_Close;

L_bye:
    mes "[Elanore the Healer]";
    mes "\"Adeus!\"";
    goto L_Close;

L_Close:
    set @Q_MASK, 0;
    set @Q_SHIFT, 0;
    set @has_magic, 0;
    set @Q_STATUS_MAX, 0;
    set @Q_heal_exp, 0;
    set @Q_status, 0;
    set @SUP_id, 0;
    set @SUP_name$, "";
    set @ignore, 0;
    set @STATUS_LEARNED_LIGHT_HEAL, 0;
    set @STATUS_MASTERED_LIGHT_HEAL, 0;
    set @STATUS_LEARNED_LAY_ON_HANDS, 0;
    set @STATUS_WAITING_FOR_KADIYA, 0;
    set @STATUS_READY_TO_LEARN_CURE_POISON, 0;
    set @STATUS_LEARNED_CURE_POISON, 0;
    set @TEMP,0;
    set @xp, 0;
    close;

S_update_var:
    set QUEST_MAGIC2,
        (QUEST_MAGIC2 & ~(@Q_MASK)
        | (@Q_status << @Q_SHIFT));
    return;
}
